buildscript {
   repositories {
      mavenCentral()
      maven { url "http://repo.spring.io/libs-release" }
   }
   dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.0.RELEASE")
   }
}


apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'

project.webAppDirName = 'WebContent'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url System.getenv()['LOCAL_MAVEN_REPO'] }
}

configurations {
   provided
   providedRuntime

}

war {  
   baseName = 'simplemq'
   rootSpec.exclude 'application-**.properties'
   
} 
eclipse.classpath.file {
    // Classpath entry for Eclipse which changes the order of classpathentries; otherwise no sources for 3rd party jars are shown
    withXml { xml ->
        def node = xml.asNode()
        node.remove( node.find { it.@path == 'org.eclipse.jst.j2ee.internal.web.container' } )
        node.appendNode( 'classpathentry', [ kind: 'con', path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
    }
}

eclipse {
   classpath {
      downloadSources = true
   }
   wtp {
      component {
         contextPath = "/simplemq"
         deployName = "simplemq"
         resource sourcePath: 'WebContent', deployPath: '/'  
      }
   }
}
 
dependencies {
   providedCompile 'org.springframework.boot:spring-boot-starter-tomcat'
   
   compile 'org.springframework.boot:spring-boot-starter-web'
   compile 'org.springframework.boot:spring-boot-starter-actuator'
   compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
   
   compile 'org.apache.commons:commons-lang3:3.3.2'
   compile 'org.ocpsoft.prettytime:prettytime:3.2.7.Final'
   
} 
